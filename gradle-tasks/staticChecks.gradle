/*
 * Copyright Â© Magento, Inc. All rights reserved.
 * See COPYING.txt for license details.
 */

import groovy.json.JsonSlurper

/**
 * Static Checks
 */

checkstyle {
    toolVersion = "8.31"
    ignoreFailures = false
    maxWarnings = 0
    configFile rootProject.file("${rootDir}/gradle-tasks/checkstyle/checkstyle.xml")
}

pmd {
    toolVersion = "6.21.0"
    consoleOutput = true
    ruleSetFiles = files("${rootDir}/gradle-tasks/pmd/ruleset.xml")
    ruleSets = []
}

tasks.withType(Checkstyle).configureEach {
    // Specify all files that should be checked
    classpath = files()
    source "${project.rootDir}"
}

// Execute Checkstyle on all files
tasks.register('checkstyle', Checkstyle) {
}

// Execute Checkstyle on all modified files
tasks.register('checkstyleCI', Checkstyle) {
    def changedFiles = getChangedFiles()
    include changedFiles
}

tasks.withType(Pmd).configureEach {
    // Specify all files that should be checked
    classpath = files()
    source "${project.rootDir}"
}

// Execute Checkstyle on all files
tasks.register('pmd', Pmd) {
}

// Execute Checkstyle on all modified files
tasks.register('pmdCI', Pmd) {
    def changedFiles = getChangedFiles()
    include changedFiles
}

/**
 * Get all files that are changed but not deleted nor renamed.
 * Compares to master or the specified target branch.
 *
 * @return list of all changed files
 */
def getChangedFiles() {
    def modifiedFilesJson = System.getenv("MODIFIED_FILES")
    List<String> files = new ArrayList<>()

    if (modifiedFilesJson == null) {
        return files
    }

    println "Modified Files: ${modifiedFilesJson}"

    //println "Changed files:"
    def modifiedFiles = new JsonSlurper().parseText(modifiedFilesJson)

    modifiedFiles.each {
        files.add(it.toString())
    }

    // Return the list of touched files
    files
}